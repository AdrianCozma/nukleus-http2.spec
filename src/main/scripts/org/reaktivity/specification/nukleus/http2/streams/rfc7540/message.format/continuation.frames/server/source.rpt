#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newSourceInputRef ${nuklei:newReferenceId()} # external scope

property nuklei ${nuklei:directory("target/nukleus-itests").streamsCapacity(1024 * 1024, 64 * 1024)}

property sourceInput ${nuklei.streams("http2", "source")}
property sourceHandshakeEst ${nuklei.streams("source", "http2#source")}
property sourceOutputEst ${nuklei.streams("source", "http2#target")}

property newSourceInputId ${nuklei:newStreamId()}
property newSourceCorrelationId ${nuklei:newCorrelationId()}

#
#
# writing to source-->nukleus (http2/source)
#
#
connect await ROUTED_INPUT
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceInput.throttle)}
        option writer ${agrona:oneToOneWriter(sourceInput.buffer)}
connected

# begin frame
write [0x00 0x00 0x00 0x01]
write ${newSourceInputId}
write ${newSourceInputRef}
write ${newSourceCorrelationId}
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceInputId}
read [0..4]

# data frame (http2 client connection preface)
write [0x00 0x00 0x00 0x02]
write ${newSourceInputId}
write [0x00 0x18]
write "PRI * HTTP/2.0\r\n"
      "\r\n"
      "SM\r\n"
      "\r\n"
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceInputId}
read [0..4]

# data frame (http2 SETTINGS frame)
write [0x00 0x00 0x00 0x02]
write ${newSourceInputId}
write [0x00 0x0f]
write [0x00 0x00 0x06]                   # length = 6
      [0x04]                             # HTTP2 SETTINGS frame
      [0x00]                             # flags = 0x00
      [0x00 0x00 0x00 0x00]              # stream_id = 0
      [0x00 0x04 0x00 0x00 0xff 0xff]    # SETTINGS_INITIAL_WINDOW_SIZE(0x04) = 65535
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceInputId}
read [0..4]

# data frame (http2 SETTINGS frame)
write [0x00 0x00 0x00 0x02]
write ${newSourceInputId}
write [0x00 0x09]
write [0x00 0x00 0x00]          # length = 0
      [0x04]                    # HTTP2 SETTINGS frame
      [0x01]                    # ACK
      [0x00 0x00 0x00 0x00]     # stream_id = 0
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceInputId}
read [0..4]

# data frame - HTTP2 HEADERS stream_id=1
write [0x00 0x00 0x00 0x02]
write ${newSourceInputId}
write [0x00 0x0b]
write [0x00 0x00 0x02]                  # length = 2
      [0x01]                            # HEADERS frame
      [0x00]                            # no flags
      [0x00 0x00 0x00 0x01]             # stream_id = 1
      [0x83]                            # :method: POST
      [0x86]                            # :scheme: http
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceInputId}
read [0..4]

# data frame - HTTP2 CONTINUATION stream_id=1
write [0x00 0x00 0x00 0x02]
write ${newSourceInputId}
write [0x00 0x1a]
write [0x00 0x00 0x11]                  # length = 17
      [0x09]                            # CONTINUATION frame
      [0x04]                            # END_HEADERS
      [0x00 0x00 0x00 0x01]             # stream_id = 1
      [0x84]                            # :path: /
      [0x01] [0x0e] "localhost:8080"    # :authority: localhost:8080
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceInputId}
read [0..4]

# data frame - HTTP2 DATA stream_id=1
write [0x00 0x00 0x00 0x02]
write ${newSourceInputId}
write [0x00 0x15]
write [0x00 0x00 0x0c]                  # length = 12
      [0x00]                            # HTTP2 DATA frame
      [0x01]                            # END_STREAM
      [0x00 0x00 0x00 0x01]             # stream_id = 1
      "Hello, world"
write flush

# window frame
read [0x40 0x00 0x00 0x02]
read ${newSourceInputId}
read [0..4]

close
closed


#
#
# reading from source-->nukleus-->source (source/http2#source)
#
#
connect
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceHandshakeEst.buffer)}
        option writer ${agrona:oneToOneWriter(sourceHandshakeEst.throttle)}
connected

# begin frame
read [0x00 0x00 0x00 0x01]
read ([0..8]:sourceOutputEstId)
read [0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00]
read ${newSourceCorrelationId}

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceOutputEstId}
write [0x00 0x02 0x00 0x00]
write flush

# data frame (server connection preface - SETTINGS frame)
read [0x00 0x00 0x00 0x02]
read ${sourceOutputEstId}
read [0x00 0x0F]
read [0x00 0x00 0x06]                   # length = 6
     [0x04]                             # HTTP2 SETTINGS frame
     [0x00]                             # flags = 0x00
     [0x00 0x00 0x00 0x00]              # stream_id = 0
     [0x00 0x03] [0x00 0x00 0x00 0x64]  # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceOutputEstId}
write [0x00 0x02 0x00 0x00]
write flush

# data frame (http2 SETTINGS frame)
read [0x00 0x00 0x00 0x02]
read ${sourceOutputEstId}
read [0x00 0x09]
read [0x00 0x00 0x00]           # length = 0
     [0x04]                     # HTTP2 SETTINGS frame
     [0x01]                     # ACK
     [0x00 0x00 0x00 0x00]      # stream_id = 0

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceOutputEstId}
write [0x17 0x00 0x00 0x00]
write flush

close
closed


#
#
# reading from target-->nukleus-->source (source/http2#target)
#
#
connect await ROUTED_OUTPUT
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceOutputEst.buffer)}
        option writer ${agrona:oneToOneWriter(sourceOutputEst.throttle)}
connected

# ========================== HTTP2 stream-id=1  =================================
# begin frame is already there in handshake stream (same tcp stream)
# nuklei data frame (contains http2 headers frame)
read [0x00 0x00 0x00 0x02]
read ${sourceOutputEstId}
read [0x00 0x61]

read [0x00 0x00 0x58]                                      # length
     [0x01]                                                # HTTP2 HEADERS frame
     [0x04]                                                # END_HEADERS
     [0x00 0x00 0x00 0x01]                                 # stream_id=1
     [0x88]                                                # :status: 200
     [0x0f 0x27] [0x14] "CERN/3.0 libwww/2.17"             # server
     [0x0f 0x12] [0x1d] "Wed, 01 Feb 2017 19:12:46 GMT"    # date
     [0x0f 0x10] [0x18] "text/html; charset=UTF-8"         # content-type
     [0x0f 0x0d] [0x02] "61"                               # content-length

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceOutputEstId}
write [0x00 0x02 0x00 0x00]
write flush

# nuklei data frame (contains http2 data frame)
read [0x00 0x00 0x00 0x02]
read ${sourceOutputEstId}
read [0x00 0x7a]
read [0x00 0x00 0x71]                          # length = 119
     [0x00]                                    # HTTP2 DATA frame
     [0x00]                                    # no flags
     [0x00 0x00 0x00 0x01]                     # stream_id=1
     "<html>"
     "<head><link rel=\"stylesheet\" href=\"styles.css\"></head>"
     "<body><script src=\"script.js\"></script></body>"
     "</html>"

# window frame
write [0x40 0x00 0x00 0x02]
write ${sourceOutputEstId}
write [0x00 0x02 0x00 0x00]
write flush

close
closed
